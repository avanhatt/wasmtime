nodes = _{ SOI ~ newline_terminated_nodes ~ EOI }

node = {
      term
    | block
    | wrapped
    | array
    | func_ident
    | ident
    | string
}

term = { ident ~ arg_list }

arg_list = {
    "(" ~ comma_delimited_nodes? ~ ")"
}

block = {
    "{" ~ NEWLINE ~ newline_terminated_nodes? ~ "}"
}

newline_terminated_nodes = _{
    (node ~ NEWLINE)+
}

wrapped = _{
    "(" ~ node ~ ")"
}

array = {
    "[" ~ semicolon_delimited_nodes? ~ "]"
}

comma_delimited_nodes = _{
    node ~ ("," ~ node)*
}

semicolon_delimited_nodes = _{
    node ~ (";" ~ node)*
}

func_ident = { ident ~ "." ~ number }

ident = @{
      name ~ "." ~ name
    | name
}

name = @{
    (ASCII_ALPHA | "_") ~
    (ASCII_ALPHANUMERIC | "_")*
}

number = @{ ASCII_DIGIT+ }

string = @{
    "\"" ~ ("\\\"" | !"\"" ~ ANY)* ~ "\""
}
